---
title: Common Vowels
challenge: 319
---
<h2 id="task-1">
Task 1: Word Count
</h2>
**Submitted by: Mohammad Sajid Anwar**

---
You are given a list of words containing alphabetic characters only.

Write a script to return the count of words either starting with a vowel or ending with a vowel.

### Example 1
```
Input: @list = ("unicode", "xml", "raku", "perl")
Output: 2

The words are "unicode" and "raku".
```
### Example 2
```
Input: @list = ("the", "weekly", "challenge")
Output: 2
```
### Example 3
```
Input: @list = ("perl", "python", "postgres")
Output: 0
```
---
### Solution
In the task's description does neither specify the terms _alphabetic character_ nor the meaning of _vowel_ in this context.
Making some assumptions to solve this task:
- Restricting _alphabetic characters_ to _Latin letters_.
- A _vowel_ being a "speech sound" is not strictly related to letters.
For this task, considering the letters `aeiou` and their derived forms as _vowels_.

Under these assumptions there are four categories of letters that can be regarded as a vowel:
- the _base vowels_ `aeiou`
- derived vowels, i.e base vowels with a modifier like diaeresis, accent etc.
- pure typographic ligatures like `ﬁ` composed of `f` and `i`
- ligatures that have evolved into a letter of its own, e.g. `æ` or `œ`

Here is a small list of Danish, French and German words that should count as starting with or ending in a vowel:`Öl`, `Bö`, `øre`, `knæ`, `œuf`, `Proﬁ`, `Ålborg`.
In contrary, words starting with or ending in a spoken vowel that are not counted are e.g. `hour`, `Yttrium` or `many`.

In the solution the _base vowels_ are simply specified as a
["Bracketed Character class"](https://perldoc.perl.org/perlrecharclass#Bracketed-Character-Classes).

Derived vowels can be identified by their _base vowel_ by canonical decomposition into
[Normalization Form D](https://metacpan.org/pod/Unicode::Normalize#$NFD_string-=-NFD($string)).
Trailing non-letter characters can be ignored.

Typographic ligatures are broken into their component letters by transforming into
["case folded"](https://perldoc.perl.org/perlfunc#fc-EXPR) form.

Finally, the individual special forms of vowels need to be added to the mentioned "Bracketed Character class".

Putting these together results in this implementation:

```perl
use v5.16;
use warnings;
use Unicode::Normalize;

sub word_count {
    scalar grep /^(?&VOWEL)|(?&VOWEL)\P{L}*$(?(DEFINE)(?<VOWEL>[aeiouøæœ]))/,
    map NFD(fc), @_;
}
```

See the [full solution]({{ page.solution_url }}/challenge-{{ page.challenge }}/jo-37/perl/ch-1.pl) to task 1.

<!--
See [discussion](https://github.com/jo-37/the-bears-den/issues/XXX
-->

<h2 id="task-2">
Task 2: Minimum Common
</h2>
**Submitted by: Mohammad Sajid Anwar**

---
You are given two arrays of integers.

Write a script to return the minimum integer common to both arrays. If none found return `-1`.

### Example 1
```
Input: @array_1 = (1, 2, 3, 4)
       @array_2 = (3, 4, 5, 6)
Output: 3

The common integer in both arrays: 3, 4
The minimum is 3.
```
### Example 2
```
Input: @array_1 = (1, 2, 3)
       @array_2 = (2, 4)
Output: 2
```
### Example 3
```
Input: @array_1 = (1, 2, 3, 4)
       @array_2 = (5, 6, 7, 8)
Output: -1
```
---
### Solution
The task may easily generalized to an arbitrary number of lists.

```perl
use strict;
use warnings;
use PDL;

sub minimum_common {
    my ($sect, @an) = map long($_), @_;
    $sect = setops $sect, 'AND', $_ for @an;
    $sect->isempty ? -1 : min $sect;
}
```
See the [full solution]({{ page.solution_url }}/challenge-{{ page.challenge }}/jo-37/perl/ch-2.pl) to task 2.

<!--
See [discussion](https://github.com/jo-37/the-bears-den/issues/XXX
-->
